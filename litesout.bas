10 'LIGHTSOUT1.BAS
20 'ROOM FOR A 5X5 GRID
30 CLS:DIM L(4,4)
40 'START NEW GAME
50 'INITIALIZE GRID "ON"
60 FOR X=0 TO 4
70 FOR Y=0 TO 4
80 L(X,Y)=-1
90 NEXT
100 NEXT
110 'INITIAL PLAYER SQUARE
120 PX=0:PY=0
130 'DRAW BOARD
140 FOR X=0 TO 4
150 FOR Y=0 TO 4
160 PRINT@X+Y*32,;:IF L(X,Y) THEN PRINT"X"; ELSE PRINT" ";
170 NEXT
180 NEXT
190 'PRINT PLAYER MARKER
200 L=PX+PY*32
210 PRINT@L,"*";:PRINT@L,;
220 A$=INKEY$
230 'PRINT LIGHT
240 IF L(PX,PY) THEN PRINT"X"; ELSE PRINT" ";
250 IF A$="" THEN 200
260 ON INSTR("*WASD ",A$) GOTO 220,280,310,340,370,400
270 'UP
280 PY=PY-1:IF PY <0 THEN PY=4
290 GOTO 200
300 'LEFT
310 PX=PX-1:IF PX<0 THEN PX=4
320 GOTO 200
330 'DOWN
340 PY=PY+1:IFP Y>4 THEN PY=0
350 GOTO 200
360 'DOWN
370 PX=PX+1:IF PX>4 THEN PX=0
380 GOTO 200
390 'TOGGLE LIGHT
400 L(PX,PY)=NOT L(PX,PY)
410 'TOGGLE SQUARE LEFT
420 IF PX>0 THEN L(PX-1,PY)=NOT L(PX-1,PY)
430 'TOGGLE SQUARE RIGHT
440 IF PX<4 THEN L(PX+1,PY)=NOT L(PX+1,PY)
450 'TOGGLE SQAURE ABOVE
460 IF PY>0 THEN L(PX,PY-1)=NOT L(PX,PY-1)
470 'TOGGLE SQUARE BELOW
480 IF PY<4 THEN L(PX,PY+1)=NOT L(PX,PY+1)
490 GOTO 140
500 'END OF PROGRAM